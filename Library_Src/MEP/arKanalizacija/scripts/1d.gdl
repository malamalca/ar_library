nix = 0.00001

ADD_FIRSTLAST_NODE_OFFSET = 0.1
MAX_POINTS = 50
MIN_SHAFT_HEIGHT = 0.1

SHOWTEXT_ALWAYS = 0
SHOWTEXT_NEVER = 1
SHOWTEXT_MVO = 2

SHAFT_DATA_OPTIONS_COUNT = 5
SHAFT_DATA_DIMENSIONS_COUNT = 10

SEGMENT_LENGTH_3D = 1
SEGMENT_LENGTH_2D = 2
SEGMENT_SLOPE = 3


!-------------------- SKUPNE KONSTANTE Z JAŠKOM -------------------------!
OPT_SHOW_SHAFT = 1
OPT_SHAFT_TYPE = 2
OPT_COVER_TYPE = 3

DIM_SHAFT_HEIGHT = 1
DIM_SHAFT_DIM1 = 2
DIM_SHAFT_DIM2 = 3
DIM_COVER_DIM1 = 4
DIM_COVER_DIM2 = 5
DIM_INLET_HEIGHT = 6
DIM_OUTLET_HEIGHT = 7
DIM_MARKER_POSX = 8
DIM_MARKER_POSY = 9

COVER_TYPE_ROUND = 0
COVER_TYPE_RECTANGLE = 1

SHAFT_TYPE_ROUND = 0
SHAFT_TYPE_RECTANGLE = 1




linear_unit = ""
ret = request("Linear_dimension", "", linear_format)

if strstr(linear_format, "e") > 0 then linear_unit = "m"
if strstr(linear_format, "m") > 0 then linear_unit = "m"
if strstr(linear_format, "mm") > 0 then linear_unit = "mm"
if strstr(linear_format, "cm") > 0 then linear_unit = "cm"
if strstr(linear_format, "ffi") > 0 then linear_unit = "ffi"
if strstr(linear_format, "fdi") > 0 then linear_unit = "fdi"
if strstr(linear_format, "df") > 0 then linear_unit = "ft"
if strstr(linear_format, "di") > 0 then linear_unit = "in"
if strstr(linear_format, "pt") > 0 then linear_unit = "pt"


!----------------------------------------
! Insert node loop
!----------------------------------------
if glob_modpar_name = "xyz_e" then
	! new node position
	dim xyz_ee[3]

	! new node index
	insi = 0

	for i = 2 to n + 1
		checkChanged = abs(xyz_ea[i][1]-xyz_e[i][1]) > nix or abs(xyz_ea[i][2]-xyz_e[i][2]) > nix or abs(xyz_ea[i][3]-xyz_e[i][3]) > nix

		if checkChanged then
			insi = i
			xyz_ee = xyz_e[i]
		endif
	next i

	if insi > 0 then
		for i = n to insi step -1
			xyz[i+1] = xyz[i]
			shaftDataOptions[i+1] = shaftDataOptions[i]
			shaftDataDimensions[i+1] = shaftDataDimensions[i]
			pipeDimensions[i+1] = pipeDimensions[i] 
		next i

		xyz[insi] = xyz_ee
		for i = 1 to SHAFT_DATA_OPTIONS_COUNT: shaftDataOptions[insi][i] = 0: next i
		for i = 1 to SHAFT_DATA_DIMENSIONS_COUNT: shaftDataDimensions[insi][i] = 0: next i
		pipeDimensions[insi] = pipeDimensions[insi-1] 

		n = n + 1
	endif
endif

if glob_modpar_name = "xyz_e" or glob_modpar_name = "xyz" then
	!----------------------------------------
	! CleanUP 
	!----------------------------------------
	n0 = 1
	put xyz[1][1], xyz[1][2], xyz[1][3]
	put shaftDataOptions[1], shaftDataDimensions[1]
	put 0 ! pipedimension

	for i = 2 to n
		dx = xyz[i][1] - xyz[i-1][1]
		dy = xyz[i][2] - xyz[i-1][2]
		dz = xyz[i][3] - xyz[i-1][3]

		rd = sqr(dx^2 + dy^2)

		if rd > nix then
			n0 = n0 + 1
			put xyz[i]
			put shaftDataOptions[i], shaftDataDimensions[i]
			put pipeDimensions[i-1]
		endif
	next i

	n = n0

	! fill up points with new values
	for i = 1 to n
		xyz[i][1] = get(1): xyz[i][2] = get(1): xyz[i][3] = get(1)
		for j = 1 to SHAFT_DATA_OPTIONS_COUNT: shaftDataOptions[i][j] = get(1): next j
		for j = 1 to SHAFT_DATA_DIMENSIONS_COUNT: shaftDataDimensions[i][j] = get(1): next j
		if i > 1 then pipeDimensions[i-1] = get(1) else tmpWaste = get(1)
	next i

	! clean empty points to the end
	for i = n + 1 to MAX_POINTS
		xyz[i] = xyz[n]
	next i

endif

! total length
dim segmentData[50][3]
totalLength = 0
totalLength2D = 0
minZValue = xyz[1][3]

for i = 2 to n
	x1 = xyz[i-1][1]
	x2 = xyz[i][1]
	y1 = xyz[i-1][2]
	y2 = xyz[i][2]
	z1 = xyz[i-1][3]
	z2 = xyz[i][3]

	if xyz[i][3] < minZValue then minZValue = xyz[i][3]
	
	segmentData[i-1][SEGMENT_LENGTH_3D] = sqr((x2-x1)^2 + (y2-y1)^2 + (z2-z1)^2)
	segmentData[i-1][SEGMENT_LENGTH_2D] = sqr((x2-x1)^2 + (y2-y1)^2)

	if abs(segmentData[i-1][SEGMENT_LENGTH_2D]) > nix then
		segmentData[i-1][SEGMENT_SLOPE] = ((z2 + shaftDataDimensions[i][DIM_INLET_HEIGHT]) - z1 - shaftDataDimensions[i-1][DIM_OUTLET_HEIGHT]) / segmentData[i-1][SEGMENT_LENGTH_2D]
	else
		segmentData[i-1][SEGMENT_SLOPE] = 0
	endif
	totalLength = totalLength + segmentData[i-1][SEGMENT_LENGTH_3D] 
	totalLength2D = totalLength2D + segmentData[i-1][SEGMENT_LENGTH_2D] 
next i	


