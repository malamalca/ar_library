uid = 1

! current points
MODEL SOLID
set building_material mat

_unused = request{2}("Building_Material_info", mat, "gs_bmat_surface", _matCut)
if override_surface then
	_mat = surf
else
	_mat = _matCut
endif

set material _mat


! debug
!GLOB_CSTORY_HEIGHT = 2.83
!GLOB_CSTORY_ELEV = 0

! chop off for floorplans
IF GLOB_CONTEXT = 2 THEN
	ADDZ (GLOB_HSTORY_ELEV + GLOB_HSTORY_HEIGHT) + (GLOB_HSTORY_HEIGHT - GLOB_ELEVATION) + GLOB_CSTORY_ELEV
	rotx 180
	CUTPLANE
	del top
	
	ADDZ (GLOB_HSTORY_ELEV + GLOB_HSTORY_HEIGHT) + (GLOB_HSTORY_HEIGHT - GLOB_ELEVATION) + GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT
	CUTPLANE
	DEL  top
ENDIF 

!debug
!addz  (GLOB_HSTORY_ELEV + GLOB_HSTORY_HEIGHT) + (GLOB_HSTORY_HEIGHT - GLOB_ELEVATION) + GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT
!CIRCLE 0.5
!del 1
!addz  (GLOB_HSTORY_ELEV + GLOB_HSTORY_HEIGHT) + (GLOB_HSTORY_HEIGHT - GLOB_ELEVATION) + GLOB_CSTORY_ELEV
!CIRCLE 0.5
!del 1


prevbAngle = 0

FOR i = 1 TO nSegments
	direction = directions[i]
	tXb = COS(direction+90)
	tYb = SIN(direction+90)
	tXe = -COS(direction+90)
	tYe = -SIN(direction+90)
	angle = angles[i]/2

	SPRISM_{2} _mat, _mat, _mat,
		12,
		tXb, tYb, tXe, tYe, segments[i], angle, 		! top vector
		-A/2, 0, A/2, 0, 0, prevbAngle,					! bottom vector
               -A/2, 0,     13+66, _mat,
        0, 0,    900,  _mat,
                   0,          180,   4013+66,  _mat,
        0, 0,    900,  _mat,
                   0,          180,   4013+66,  _mat,
               -A/2, 0,     -1,  _mat,
                A/2 - wallThickness, 0,     13+66,  _mat,
        0, 0,    900+66,  _mat,
                   0,         -180,   4013+66,  _mat,
        0, 0,    900,  _mat,
                   0,         -180,   4013+66,  _mat,
                A/2 - wallThickness, 0,     -1, _mat

	prevbAngle = angle 

	! length editing
	HOTSPOT 0, 0, 0, uid, segments[i], 1: uid = uid + 1
	HOTSPOT 0, 0, segments[i], uid, segments[i], 2: uid = uid + 1
	HOTSPOT 0, 0, -1, uid, segments[i], 3: uid = uid + 1

	! direction editing
	HOTSPOT A/2, 0, segments[i], uid, directions[i], 4: uid = uid + 1
	HOTSPOT COS(directions[i])*A/2, SIN(directions[i])*A/2, segments[i], uid, directions[i], 5: uid = uid + 1
	HOTSPOT 0, 0, segments[i], uid, directions[i], 6: uid = uid + 1
	HOTSPOT 0, 0, segments[i]+1, uid, directions[i], 7: uid = uid + 1

	ADDZ segments[i]

	! angle editing - at half of segment length
	angleSpotPos = segments[i+1]/2

	HOTSPOT 0, 0, angleSpotPos, uid, angles[i], 4: uid = uid + 1
	HOTSPOT COS(directions[i])*angleSpotPos*SIN(angles[i]), SIN(directions[i])*angleSpotPos*SIN(angles[i]), angleSpotPos*COS(angles[i]), uid, angles[i], 5: uid = uid + 1
	HOTSPOT 0, 0, 0, uid, angles[i], 6: uid = uid + 1
	HOTSPOT 0, 1, 0, uid, angles[i], 7: uid = uid + 1
	
	!! debug
	!CIRCLE 0.1
	!LIN_ tXb, tYb, 0, tXe, tYe, 0
	
	HOTSPOT COS(direction)*A, SIN(direction)*A, 0

	ROTZ 90+direction
	ROTX angles[i]

	!! debug
	!LIN_ prevbXb, prevbYb , 0, prevbXe , prevbYe , 0
NEXT i


IF GLOB_CONTEXT = 2 THEN
	CUTEND 
	CUTEND
ENDIF 
