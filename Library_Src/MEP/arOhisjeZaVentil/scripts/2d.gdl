if iKind = KIND_EXHAUST then
	pen penExhaust
else
	pen penIntake
endif

showIn2D = 1


if representation2D = DISPLAY_MVO then
	n = REQUEST ("Home_story", "", home_story_index, home_story_name)
	n = REQUEST ("Story", "", current_story_index, current_story_name)

	if home_story_index = current_story_index then
		_bSuccess = libraryglobal("arMVO", "arPrikazVentilaHomeStory", representationMVO)
	else
		_bSuccess = libraryglobal("arMVO", "arPrikazVentilaOtherStory", representationMVO)
	endif

	
	if _bSuccess then
		if representationMVO = 3 then
			showIn2D = 0
		endif
		if representationMVO = 1 then
			representation2D = DISPLAY_SIMPLE
		endif
		if representationMVO = 2 then
			representation2D = DISPLAY_DETAILED
		endif
	endif
endif


if showIn2D = 1 then
	if representation2D = DISPLAY_SIMPLE then
		circle2 0, 0, A/2
	else
		IF iKind = KIND_EXHAUST THEN 
			IF iBuiltin = BUILTIN_CEILING THEN
				FRAGMENT2 2, 1
			ELSE
				FRAGMENT2 4, 1
			ENDIF
		ELSE
			IF iBuiltin = BUILTIN_CEILING THEN
				FRAGMENT2 1, 1
			ELSE
				FRAGMENT2 3, 1
			ENDIF
		ENDIF
	endif


	!CIRCLE2 0, 0, A/2
	!CIRCLE2 0, 0, A/2 - wallThickness

	HOTSPOT2 0, 0

	IF showVolumeText THEN
		DEFINE STYLE "Internal" text_font, text_size / GLOB_SCALE * 10, 5, 0
		ret = REQUEST ("Linear_dimension", "", dim_format)
		string = STR(airVolume, 3, 0) + "m3/h"

		! move to position for text output
		ADD2 text_pos_x, text_pos_y
		ROT2 -text_angle
		
		SET STYLE "Internal"
		string_w = STW(string) / 1000 * A_
		TEXT2 0, 0, string
		DEL 2


		! text hotspots
		HOTSPOT2 text_pos_x, 0, 3, text_pos_y, 1+128
		HOTSPOT2 text_pos_x, -1, 4, text_pos_y, 3
		HOTSPOT2 text_pos_x, text_pos_y, 5, text_pos_y, 2
		
		HOTSPOT2 0, text_pos_y, 6, text_pos_x, 1+128
		HOTSPOT2 -1, text_pos_y, 7, text_pos_x, 3
		HOTSPOT2 text_pos_x, text_pos_y, 8, text_pos_x, 2
		
		! angle hotspots
		HOTSPOT2 text_pos_x + (string_w/2),						text_pos_y, 									9,	text_angle, 4+128
		HOTSPOT2 text_pos_x + (string_w/2) * COS(text_angle),	text_pos_y - (string_w/2) * SIN(text_angle),	10,	text_angle, 5
		HOTSPOT2 text_pos_x, 									text_pos_y,										11,	text_angle, 6 + 512
		HOTSPOT2 text_pos_x + (string_w/2),						0,												12, text_angle, 7

	ENDIF
endif