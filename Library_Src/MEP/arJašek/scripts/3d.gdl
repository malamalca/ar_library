model solid
pen SYMB_VIEW_PEN
material matUnderground

unId = 1

hotspot 0, 0, 0, unId, ZZYZX, 1: unId = unId + 1
hotspot 0, 0, ZZYZX, unId, ZZYZX, 2: unId = unId + 1
hotspot 0, 0, -1, unId, ZZYZX, 3: unId = unId + 1

hotspot deltaX, deltaY, ZZYZX, unId: unId = unId + 1


shaftHeight = ZZYZX - latchExtension

! spodnja plošča
if abs(bottomThickness) > NIX then
	shaftHeight = shaftHeight - bottomThickness
	if kind = KIND_GENERIC_ROUND or kind = KIND_GENERIC_STANDARD then
		cylind bottomThickness, A/2
	endif
	if kind = KIND_GENERIC_RECTANGLE then
		prism_ 5, bottomThickness,
			-A/2, -B/2, 15,
			A/2, -B/2, 15,
			A/2, B/2, 15,
			-A/2, B/2, 15,
			-A/2, -B/2, -1
	endif
	addz bottomThickness
endif


if abs(topThickness) > NIX then
	shaftHeight = shaftHeight - topThickness
	addz shaftHeight

	!! zgornja plošča
	nNodes = 0
	if kind = KIND_GENERIC_ROUND or kind = KIND_GENERIC_STANDARD then
		put -A/2,0, 13+66
		put 0,0,900
		put 0, 180, 4000+13+66
		put 0,0,900
		put 0, 180, 4000+13+66
		put -A/2, 0, -1
		nNodes = 6
	endif
	if kind = KIND_GENERIC_RECTANGLE then
		put -A/2, -B/2, 15
		put A/2, -B/2, 15
		put A/2, B/2, 15
		put -A/2, B/2, 15
		put -A/2, -B/2, -1
		nNodes = 5
	endif

	!! izrez v zgornji plošči
	if latchKind = LATCH_ROUND then
		put deltaX,			deltaY,	900 ! set centerpoint
		put latchSize/2, 360, 4013
		nNodes = nNodes + 2
	endif

	if nNodes > 0 then
		prism_ nNodes, topThickness, get(nNodes * 3)
	endif

	!! povišek
	if abs(latchExtension) > NIX then
		addz topThickness
		hotspot deltaX, deltaY, 0, unId, latchExtension, 1: unId = unId + 1
		hotspot deltaX, deltaY, latchExtension, unId, latchExtension, 2: unId = unId + 1
		hotspot deltaX, deltaY, -1, unId, latchExtension, 3: unId = unId + 1

		add deltaX, deltaY, 0
		prism_ 12, latchExtension,
			-latchSize/2 - latchWallWidth,0, 13+66,
			0,0,900,
			0, 180, 4000+13+66,
			0,0,900,
			0, 180, 4000+13+66,
			-latchSize/2 - latchWallWidth, 0, -1,
			-latchSize/2, 0, 13,
			0,0,900,
			0, -180, 4013,
			0,0,900,
			0,-180, 4013,
			-latchSize/2 - latchWallWidth, 0, -1
		del 2
	endif

	del 1
endif

if kind = KIND_GENERIC_ROUND or kind = KIND_GENERIC_STANDARD then
	prism_ 12, shaftHeight,
		-A/2,0, 13+66,
		0,0,900,
		0, 180, 4000+13+66,
		0,0,900,
		0, 180, 4000+13+66,
	
		-A/2, 0, -1,
		A/2-wallWidth, 0, 13,
		0,0,900,
		0, -180, 4013,
		0,0,900,
		0,-180, 4013,
		A/2-wallWidth,0,-1
endif

wallWidth = 0.001
if kind = KIND_GENERIC_RECTANGLE then
	prism_ 10, shaftHeight,
		-A/2, -B/2, 15,
		A/2, -B/2, 15,
		A/2, B/2, 15,
		-A/2, B/2, 15,
		-A/2, -B/2, -1,
	
		-A/2+wallWidth, -B/2+wallWidth, 15,
		A/2-wallWidth, -B/2+wallWidth, 15,
		A/2-wallWidth, B/2-wallWidth, 15,
		-A/2+wallWidth, B/2-wallWidth, 15,
		-A/2+wallWidth, -B/2+wallWidth, -1
endif