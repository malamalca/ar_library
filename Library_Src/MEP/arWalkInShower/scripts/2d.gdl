pen pen2Dsnaitarije
fill 65

gosub "Baza sa fillom":
	add2 0,-b/2+pomakOdRuba
	rot2 -90+rotSifon
gosub "Sifon":
	del 1
gosub "Simbol":


!==================================!
!============ NAPOMENA ============!
!==================================!
!	U 2D PRIKAZUJEMO SAMO MASKU KOJA JE maskaA x maskaB(6 cm)----------!
!	ZA ZIDNU JE MICEMO OD DOLJNJE LINIJE ZA 0.1 CM (DEBLJINA LIMIĆA)---!
!	ZA STANDERDNU SMAO POMAK OD RUBA-----------------------------------!			
!==================================!
!============ NAPOMENA ============!
!==================================!

	if odabirKanalica >=7 then !AKO JE ZIDNA
		del 1
		add2 0,-b/2+pomakOdRuba2D 
	else
	endif
gosub "Maska":
	del 1

!========== HOTSPOT pomakOdRuba==========!
if odabirKanalica <=6 then !AKO NIJE ZIDNA
	add2 0,-b/2+pomakOdRuba
   	unID=001
   hotspot2 0,-pomakOdRuba,unID,pomakOdRuba,1+128		!--- BASE
   		unID=unID+1		
   hotspot2 0,0,unID,pomakOdRuba,2					!--- MOVEING
   		unID=unID+1
   hotspot2 0,-b*2,unID,pomakOdRuba,3					!--- REFERENCE
   		unID=unID+1
endif
!========== HOTSPOT pomakOdRuba==========!


!===================================================================!
!================ T O Č K A   Z A   R O T A C I J U ================!
!===================================================================!
del top
add2 0, -B/2 + pomakOdRuba
hotspot2	1, 0, unID, rotSifon, 4+128: unID = unID + 1			!-- BASE
hotspot2	cos(rotSifon)*sifonTockaX, sin(rotSifon)*sifonTockaX, unID, rotSifon, 5: unID = unID + 1			!-- MOVEING
hotspot2	0, 0, unID, rotSifon, 6: unID = unID + 1			!-- CENTAR
!===================================================================!
!================ T O Č K A   Z A   R O T A C I J U ================!
!===================================================================!


!!===========================================================================!
!!================ M E P   C O N N E C T I O N   E L E M N T ================!
!!===========================================================================!
del top
call "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
	MEP_NumberConnections	= MEP_NumberConnections,
	MEP_NumConnectionData	= gs_Connections,
	gs_AddConnections	= gs_AddConnections,
	DrawingNum		= 1,
	MEP_InsShow		= 0,
	MEP_cline_show	= 0,
	gs_cont_pen		= gs_cont_pen,
	gs_fill_type	= gs_fill_type,
	gs_fill_pen		= gs_fill_pen,
	gs_back_pen		= gs_back_pen,
	ConPosX_1		= ConPosX_1,
	ConPosY_1		= ConPosY_1, 
	ConLength_1		= ConLength_1, 
	ConShow_1		= 1, 
	ConEdit_1 		= 0
!!===========================================================================!
!!================ M E P   C O N N E C T I O N   E L E M N T ================!
!!===========================================================================!

end:!====================================================================================================================================================


"Baza sa fillom":
   ! ==================== B A Z A ====================!
       poly2_b{5}      5,1+2+4,0,3,              !n, frame_fill, fillcategory, distortion_flags,
                       255,255,                 !fill_pen, fill_background_pen,
                       0,0,                    !fillOrigoX, fillOrigoY,
                       1,0,0,1,                !mxx, mxy, myx, myy,
                       0,                      !gradientInnerRadius

                       -a/2,b/2,1, 
                       a/2,b/2,1,
                       a/2,-b/2,1,
                       a/2,-b/2,1,
                       -a/2,-b/2,1

   ! ==================== H O T S P O T S ====================!
       hotspot2    -a/2,b/2
       hotspot2    a/2,-b/2
       hotspot2    0,0
       hotspot2    -a/2,-b/2
       hotspot2    a/2,b/2
   ! ==================== H O T S P O T S ====================!


   ! ==================== H O T L I N E S ====================!
       hotline2    -a/2,b/2,a/2,b/2
       hotline2    a/2,-b/2,-a/2,-b/2  
       hotline2    -a/2,b/2,-a/2,-b/2
       hotline2    a/2,b/2,a/2,-b/2
   ! ==================== H O T L I N E S ====================!
return


"Sifon":
	circle2 0,0,0.060/2
	hotarc2 0,0,0.060/2,0,360

	!pen pen2Dkanalizacija
	!line2 promjerSifona/2,0,promjerSifona/2,0.060
	!line2 -promjerSifona/2,0,-promjerSifona/2,0.060
	!pen pen2Dsnaitarije
return

"Simbol":
   odmakOdCentra=0.12
   ! ==================== GLAVNI TROKUT GORE ====================!
   hipotenuzaGlavna=SQR( (a/2)^2+(b-pomakOdRuba)^2)		!--- Duzina originalne hipotenuze
   kutA=ASN((a/2)/hipotenuzaGlavna)
   kutB=90-kutA
   ! ==================== GLAVNI TROKUT GORE ====================!

   ! ==================== KOORDINATA DOLJNJE TOČKE ====================!
   hipotenuzaDolje=odmakOdCentra						!--- Duzina smanjna linije na rubu
   linijaX1=hipotenuzaDolje*SIN(kutA)
   linijaY1=SQR(hipotenuzaDolje^2-linijaX1^2)
   ! ==================== KOORDINATA DOLJNJE TOČKE ====================!

   ! ==================== KOORDINATA GORNJE TOČKE ====================!
   hipotenuzaGore=hipotenuzaGlavna-odmakOdCentra
   linijaX2=hipotenuzaGore*SIN(kutA)
   linijaY2=SQR(hipotenuzaGore^2-linijaX2^2)
   ! ==================== KOORDINATA GORNJE TOČKE ====================!





   ! ==================== GLAVNI TROKUT DOLJE ====================!
   duzinaLinijeDolje=SQR( (a/2)^2+(-pomakOdRuba)^2)		!--- Duzina originalne hipotenuze
   kutADolje=ASN((a/2)/duzinaLinijeDolje)
   kutBDolje=90-kutA
   ! ==================== GLAVNI TROKUT DOLJE ====================!

   ! ==================== KOORDINATA GORNJE TOČKE ====================!
   hipotenuzaGore=odmakOdCentra
   linijaX3=hipotenuzaGore*SIN(kutADolje)
   linijaY3=SQR(hipotenuzaGore^2-linijaX3^2)
   ! ==================== KOORDINATA GORNJE TOČKE ====================!

   ! ==================== KOORDINATA GORNJE TOČKE ====================!
   hipotenuzaGore=duzinaLinijeDolje-odmakOdCentra
   linijaX4=hipotenuzaGore*SIN(kutADolje)
   linijaY4=SQR(hipotenuzaGore^2-linijaX4^2)
   ! ==================== KOORDINATA GORNJE TOČKE ====================!



   if pomakOdRuba <= b-0.30 then  
   	line2 linijaX1,linijaY1,linijaX2,linijaY2
   	line2 0,b-pomakOdRuba-odmakOdCentra,0,odmakOdCentra
   	line2 -linijaX1,linijaY1,-linijaX2,linijaY2
   endif

   if pomakOdRuba > 0.29 then
   	line2 linijaX3,-linijaY3,linijaX4,-linijaY4
   	line2 0,-odmakOdCentra,0,-pomakOdRuba+odmakOdCentra
   	line2 -linijaX3,-linijaY3,-linijaX4,-linijaY4
   endif
return

"Maska":
	rect2 -maskaA/2,-maskaB/2,maskaA/2,maskaB/2

	hotline2 -maskaA/2,-maskaB/2,maskaA/2,-maskaB/2
	hotline2 maskaA/2,-maskaB/2,maskaA/2,maskaB/2
	hotline2 -maskaA/2,maskaB/2,maskaA/2,maskaB/2
	hotline2 -maskaA/2,-maskaB/2,-maskaA/2,maskaB/2
return