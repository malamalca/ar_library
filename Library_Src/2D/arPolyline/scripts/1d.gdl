nix = 0.00001
MAX_POINTS = 50

OFS_KIND_CUSTOM = 0
OFS_KIND_CENTER = 1
OFS_KIND_LEFT = 2
OFS_KIND_RIGHT = 3

if glob_modpar_name = "xe" or glob_modpar_name = "ye" then
	insi = 0

	for i = 1 to n+1
		if i > 1 then
			xins = (x[i-1] + x[i]) / 2
			yins = (y[i-1] + y[i]) / 2
		endif

		if (abs(xe[i]) > nix or abs(ye[i]) > nix) and (abs(xea[i] - xe[i]) > nix or abs(yea[i] - ye[i]) > nix) then
			insi = i
			xee = xe[i]
			yee = ye[i]
		endif
	next i

	if insi > 0 then
		for i = n to insi step -1
			x[i+1] = x[i]
			y[i+1] = y[i]
		next i

		x[insi] = xee
		y[insi] = yee

		n = n + 1
	endif
endif

!----------------------------------------
! CleanUP
!----------------------------------------

n0 = 0
total_length = 0

for i = 1 to n
	if i = 1 then
		dx = x[i] - x[n]
		dy = y[i] - y[n]
	else
		dx = x[i] - x[i-1]
		dy = y[i] - y[i-1]
	endif

	rd = sqr(dx^2 + dy^2)

	if rd > nix then
		n0 = n0 + 1
		put x[i], y[i]

		! do not include last length in total length
		! because last length contains distance from the last point to the first
		if i > 1 then
			total_length = total_length + rd
		endif
	endif
next i

n = n0

! fill up points with new values
for i = 1 to n
	x[i] = get(1)
	y[i] = get(1)
next i

! clean empty points to the end
for i = n + 1 to 50
	x[i] = x[n]
	y[i] = y[n]
next i



