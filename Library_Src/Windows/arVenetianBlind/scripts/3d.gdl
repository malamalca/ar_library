thk = 0.002			! metal thickness
unId = 1

openingWidth = A - sideOffset * 2

set material WALL_MAT_EDGE

insuThk = insulationThickness
if not(bInsulation) then
	insuThk = 0
endif

window_sill = WIDO_SILL + insuThk

addz insuThk
wallniche 5, 1, 2,
	0, 0, -1,
	window_sill,
	-openingWidth/2, 0, 15,
	openingWidth/2, 0, 15,
	openingWidth/2, B, 15,
	-openingWidth/2, B, 15,
	-openingWidth/2, 0, -1

del 1

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! WALL niche for blinds box
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
addz insuThk
wallniche 5, 1, 2,
	0, 0, -1,
	boxWidth + insuThk,									! depth of niche
	-openingWidth/2, 	B, 				1+2+4+8+16,
	openingWidth/2, 	B, 				1+2+4+8+16,
	openingWidth/2, 	B + boxHeight, 	1+2+4+8+16,
	-openingWidth/2, 	B + boxHeight, 	1+2+4+8+16,
	-openingWidth/2, 	B, 				-1
del 1


sideExt = WIDO_SILL - boxWidth - deltaFrontSideExt

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! WALL niche box flashing
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
addz -boxWidth
wallniche 5, 1, 2,
	0, 0, -1,
	sideExt,									! depth of niche
	-openingWidth/2, 	B, 				1+2+4+8+16,
	openingWidth/2, 	B, 				1+2+4+8+16,
	openingWidth/2, 	B + thk, 		1+2+4+8+16,
	-openingWidth/2, 	B + thk,	 	1+2+4+8+16,
	-openingWidth/2, 	B, 				-1
del 1

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! BLINDS BOX
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
set building_material boxMat

addx -openingWidth/2
addy B
roty 90



!innerExt = ac_revealToWallCore
!if (innerExt > 0) then
	innerExt = -insulationThickness
!endif


prism_ 13, openingWidth,
	0, 				0, 				15,
	innerExt, 		0,		15,
	innerExt, 		thk,		15,
	0,				thk,		15,
	0,				boxHeight,		15,
	boxWidth,		boxHeight,		15,
	boxWidth,		thk,			15,
	boxWidth+sideExt,  thk,			15,
	boxWidth+sideExt,  0,				15,
	boxWidth-thk, 	0,				15,
	boxWidth-thk, 	boxHeight-thk, 	15,
	thk,			boxHeight-thk,	15,
	thk,			0,				15


if bInsulation then
	set building_material insulationMat

	prism_ 4, openingWidth,
		-insuThk, 	thk, 				15,
		-insuThk,	boxHeight,		15,
		0,			boxHeight,		15,
		0,			thk,				15
endif

del top

!verticalOffset = 0.085
verticalOffset = boxWidth/2

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! MOTOR box
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
set building_material boxMat


motorBoxWidth = 0.065
motorBoxHeight = 0.065
motorBoxTopOffset = 0.01
motorBoxOffset = - motorBoxWidth/2 + verticalOffset + insideOffset			! offset from reveal
motorBoxTubeWidth = 0.04
motorBoxTubeThickness = 0.002

addx -openingWidth/2
addy B + boxHeight - motorBoxHeight - motorBoxTopOffset
addz -motorBoxOffset
roty 90

prism_ 8, openingWidth,
	0, 					0, 						15,
	0,					motorBoxHeight ,		15,
	motorBoxWidth,		motorBoxHeight ,		15,
	motorBoxWidth,		0,						15,
	motorBoxWidth-thk, 	0,						15,
	motorBoxWidth-thk, 	motorBoxHeight -thk, 	15,
	thk,				motorBoxHeight -thk,	15,
	thk,				0,						15

addy motorBoxHeight/2
addx motorBoxWidth/2
prism_ 12, openingWidth,
	-motorBoxTubeWidth/2,0, 13+66,
	0,0,900,
	0, 180, 4000+13+66,
	0,0,900,
	0, 180, 4000+13+66,

	-motorBoxTubeWidth/2, 0, -1,
	motorBoxTubeWidth/2-motorBoxTubeThickness, 0, 13,
	0,0,900,
	0, -180, 4013,
	0,0,900,
	0,-180, 4013,
	motorBoxTubeWidth/2-motorBoxTubeThickness,0,-1

del top

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! SIDE rail profiles
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! railBottomOffset = 0.03 ! defined by parameter
railPWidth = 0.023
railPHeight = 0.027
railOffset = -railPWidth/2 + verticalOffset + insideOffset
railHeight = B + boxHeight - 0.1 - railBottomOffset
railCorrectionOffset = 0.002 					! this is offset of rail towards center of window; prevents footing from subtraction of lower part of rail

if bRail then
	set building_material railMat

	addy railBottomOffset

	rotz 90
	roty 90
	addy openingWidth/2 - railCorrectionOffset
	addx railOffset

	gosub "sideProfile"
	del 4

	rotz -90
	roty 90
	addy openingWidth/2 - railCorrectionOffset
	addz -railHeight
	addx railOffset

	gosub "sideProfile"
endif

del top

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! SLATS
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
slatSpacing = slatWidth
slatCount = round_int((B-slatBottomOffset)/slatSpacing) + 1
slatCountFolded = round_int((B-loweredDistance-slatBottomOffset)/slatSpacing) + 1

addy slatBottomOffset
roty 90
addz -openingWidth/2 + slatSideOffset
addx railOffset + railPWidth / 2

! first slatella
addy B - loweredDistance - slatBottomOffset
if slatsKind = KIND_C then
	gosub "slatC"
else
	gosub "initSlatProfile"
	gosub "slatProfile"
endif
del 1

for i = 2 to slatCount
	dotPos = B - loweredDistance  + (slatCountFolded-1) * slatFoldedSpacing
	curSlatPos = (i-1) * slatSpacing - slatBottomOffset

	if curSlatPos < dotPos then
		! folded slat
		curSlatAngle = 0
		curSlatPos = B - loweredDistance - slatBottomOffset + (i-1) * slatFoldedSpacing
	else
		curSlatAngle = slatAngle
	endif

	addy curSlatPos
	rotz curSlatAngle

	if slatsKind = KIND_C then
		gosub "slatC"
	else
		gosub "slatProfile"
	endif

	del 2
next i
del top


!!  KOLIKO SO LAMELE SPUŠČENE
hotspot 0,  B,  					-verticalOffset - insideOffset, unId, loweredDistance, 1: unId = unId + 1
hotspot 0,  B - loweredDistance,  	-verticalOffset - insideOffset, unId, loweredDistance, 2: unId = unId + 1
hotspot 0,  B+1,  					-verticalOffset - insideOffset, unId, loweredDistance, 3: unId = unId + 1

hotspot 0,  B - loweredDistance + (slatCountFolded) * slatFoldedSpacing,  -verticalOffset - insideOffset, unId: unId = unId + 1

!! VIŠINA BOKSA
hotspot 0,  B,  					-verticalOffset - insideOffset, unId, boxHeight, 1: unId = unId + 1
hotspot 0,  B + boxHeight,  		-verticalOffset - insideOffset, unId, boxHeight, 2: unId = unId + 1
hotspot 0,  B-1,  					-verticalOffset - insideOffset, unId, boxHeight, 3: unId = unId + 1

!! ŠIRINA BOKSA
hotspot 0,  B + boxHeight,  		0, 								unId, boxWidth, 1+128: unId = unId + 1
hotspot 0,  B + boxHeight,  		-boxWidth, 						unId, boxWidth, 2: unId = unId + 1
hotspot 0,  B + boxHeight, 			+1, 							unId, boxWidth, 3: unId = unId + 1


! nekateri hotspoti so smiselni le v prerezih
if GLOB_VIEW_TYPE = 4 then
	!! ODMIK TRAČNICE LEVO/DESNO
	hotspot A/2,  0,  					-verticalOffset - insideOffset, unId, railBottomOffset, 1+128: unId = unId + 1
	hotspot A/2,  railBottomOffset,  	-verticalOffset - insideOffset, unId, railBottomOffset, 2: unId = unId + 1
	hotspot A/2,  -1,  					-verticalOffset - insideOffset, unId, railBottomOffset, 3: unId = unId + 1

	!! DEBELINA IZOLACIJE
	hotspot 0,  B + boxHeight,  		0, 								unId, insulationThickness, 1+128: unId = unId + 1
	hotspot 0,  B + boxHeight,  		insulationThickness, 			unId, insulationThickness, 2: unId = unId + 1
	hotspot 0,  B + boxHeight, 			-1, 							unId, insulationThickness, 3: unId = unId + 1

	!! OFFSET OD REVEALA DO OMARICE
	hotspot 0,  B + boxHeight - 0.1,  	-boxWidth/2, 				unId, insideOffset, 1+128: unId = unId + 1
	hotspot 0,  B + boxHeight - 0.1,  	-boxWidth/2-insideOffset, 	unId, insideOffset, 2: unId = unId + 1
	hotspot 0,  B + boxHeight - 0.1, 	-boxWidth/2 + 1, 			unId, insideOffset, 3: unId = unId + 1

	!! OFFSET PLEHA NAZAJ OD FASADE - da ni linije na fasadi
	hotspot 0,  B+thk,  				-WIDO_SILL, 						unId, deltaFrontSideExt, 1+128: unId = unId + 1
	hotspot 0,  B+thk,  				-WIDO_SILL + deltaFrontSideExt, 	unId, deltaFrontSideExt, 2: unId = unId + 1
	hotspot 0,  B+thk, 					-WIDO_SILL - 1, 					unId, deltaFrontSideExt, 3: unId = unId + 1
endif

end


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! Side profile for blind
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
"sideProfile":
	prism_ 8, railHeight,
		0, 				0, 					15,
		0,				railPHeight,		15,
		railPWidth,		railPHeight,		15,
		railPWidth,		0,					15,
		railPWidth-thk, 0,					15,
		railPWidth-thk, railPHeight-thk, 	15,
		thk,			railPHeight-thk,	15,
		thk,			0,					15

	return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! SUB: Slat C type
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
"slatC":
	addx - slatWidth / 2

	prism_ 11, openingWidth - 2 * slatSideOffset,
		0, 0, 15,

		slatWidth * 1/4, 0.002, 15+64,
		slatWidth/2, 0.0027, 15+64,
		slatWidth * 3/4, 0.002, 15+64,

		slatWidth, 0, 15,
		slatWidth, slatThickness, 15,

		slatWidth * 3/4, slatThickness + 0.002, 15+64,
		slatWidth/2, slatThickness + 0.0027, 15+64,
		slatWidth * 1/4, slatThickness + 0.002, 15+64,


		0, slatThickness, 15,
		0, 0, -1
	del 1

	return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! SUB: INIT Slat profile
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
"initSlatProfile":
	_nComponents = 0
	dim _componentTypes[]
	n = REQUEST ("Profile_components", slatProfile, _nComponents, _componentTypes)

	DIM _prismPointsX[][]
	DIM _prismPointsY[][]
	DIM _prismPointsStatus[][]


	IF _nComponents > 0 THEN
		DIM componentPointCounts[ ]
		DIM componentPoints[ ]

		n = REQUEST ("Profile_default_geometry", slatProfile, componentPointCounts)

		_pointIndex = _nComponents

		FOR _i = 1 TO _nComponents
			FOR _j = 1 TO componentPointCounts[_i]
				X = 				componentPointCounts[_pointIndex + 1]
				Y = 				componentPointCounts[_pointIndex + 2]
				edgeVisible = 		componentPointCounts[_pointIndex + 3]
				vertEdgeVisible = 	componentPointCounts[_pointIndex + 4]
				additionalStatus = 	componentPointCounts[_pointIndex + 5]

				_pointIndex = _pointIndex + 5

				prismStatus = additionalStatus
				if additionalStatus >= 0 then ! not contour end
					if edgeVisible then
						prismStatus = prismStatus + 15 ! j1, j2, j3, j4
					endif
					if vertEdgeVisible = 0 then
						prismStatus = prismStatus + 64 ! j7
						! in tube, lateral edges starting from the node are used for showing the contour
						tubeStatus = tubeStatus + 1
					endif
				endif

				!PUT X, Y, prismStatus 
				_prismPointsX[_i][_j] = X
				_prismPointsY[_i][_j] = Y
				_prismPointsStatus[_i][_j] = prismStatus
			NEXT _j
		NEXT _i
	ENDIF
return


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! SUB: Slat profile
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
"slatProfile":		
	FOR _i = 1 TO _nComponents
		_componentMaterial = 0
		n = REQUEST{4} ("Profile_component_info", slatProfile, _i, "gs_profile_bmat", _componentMaterial)	
		SET BUILDING_MATERIAL _componentMaterial
		
		FOR _j = 1 TO componentPointCounts[_i]
			put _prismPointsX[_i][_j], _prismPointsY[_i][_j], _prismPointsStatus[_i][_j]
		next _j

		PRISM_ componentPointCounts[_i], openingWidth - 2 * slatSideOffset, get(componentPointCounts[_i] * 3)
	next _i
return
