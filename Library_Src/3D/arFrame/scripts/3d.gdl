paramDrawBeamLength = A

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Bottom Element
SET MATERIAL bottomElementMaterial
paramDrawBeamWidth = bottomElementHeight

! show only if checked
if lblBottomElement then
	GOSUB "drawBeam3d"
endif

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Top element
SET MATERIAL topElementMaterial
paramDrawBeamWidth = topElementHeight


hotspot 0,0, 0, unID, cutHeight, 1 : unID = unID+1
hotspot 0,0, cutHeight, unID, cutHeight, 2 : unID = unID+1
hotspot 0,0, -1, unID, cutHeight, 3 : unID = unID+1

if cutHeight > NIX and cutHeight < ZZYZX then
	! višina poševnine (notranja in spodnja stran)
	deltaHeight = (ZZYZX - topElementHeight) - (cutHeight - topElementHeight / cos(cutAngle))

	! dolžina podaljška za fillet zgornjega kota
	deltaLen = tan(cutAngle/2) * topElementHeight


	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
	! prvi del - poševnina
	paramDrawBeamLength = deltaHeight / sin(cutAngle) + deltaLen

	
	! bottom and top cutting plane
	bottomCuttingPlaneAngle = 90
	topCuttingPlaneAngle = -90 + cutAngle/2
	topCuttingX = deltaHeight / tan(cutAngle)

	if paramDrawBeamLength > 0 then
		! when cutHeight
		if ((ZZYZX - cutHeight) / tan(cutAngle) + deltaLen) > A then
			paramDrawBeamLength = A / cos(cutAngle) + tan(cutAngle) / topElementHeight
			topCuttingPlaneAngle = -90
			topCuttingX = A
		endif
	
		addz cutHeight - topElementHeight / cos(cutAngle)

		add 0, 0, 0: rotz -90 : cutplane{2} bottomCuttingPlaneAngle : del 2								! start cut plane
		add topCuttingX, 0, deltaHeight : rotz -90 : cutplane{2} topCuttingPlaneAngle : del 2
	
		roty -cutAngle 
		GOSUB "drawBeam3d"
	
		cutend
		cutend
	
		del 2
	endif
	
	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
	! drugi del - raven
	paramDrawBeamLength = A - deltaHeight / tan(cutAngle) + deltaLen

	if paramDrawBeamLength > deltaLen then

	addz ZZYZX - topElementHeight
	addx deltaHeight / tan(cutAngle) - deltaLen
	add deltaLen, 0, 0: rotz -90 : cutplane{2} 90+cutAngle/2: del 2								! start cut plane
	GOSUB "drawBeam3d"
	cutend
	del 2
	endif

	! reset na osnovno dolžino
	paramDrawBeamLength = A


else
	ADDZ ZZYZX - topElementHeight
	GOSUB "drawBeam3d"
	DEL 1
endif

HOTSPOT 0,0,0, unID : unID = unID+1
HOTSPOT 0,0,ZZYZX, unID : unID = unID+1
HOTSPOT A,0,0, unID : unID = unID+1
HOTSPOT A,0,ZZYZX, unID : unID = unID+1

HOTSPOT 0,0,bottomElementHeight, unID : unID = unID+1
HOTSPOT 0,0,ZZYZX-topElementHeight, unID : unID = unID+1
HOTSPOT A,0,bottomElementHeight, unID : unID = unID+1
HOTSPOT A,0,ZZYZX-topElementHeight, unID : unID = unID+1

ADDZ bottomElementHeight



paramDrawStudHeight = ZZYZX- topElementHeight -bottomElementHeight


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Draw In-Between Studs

if cutHeight > NIX then
	addz cutHeight - topElementHeight - topElementHeight / cos(cutAngle)
	rotz -90
	cutplane{2} cutAngle
    del 2
endif

FOR i = 1 TO n
	SET MATERIAL defaultStudMaterial
	paramDrawStudWidth = defaultStudWidth

	HOTSPOT 0, 0, 0, unID, X[i], 1+128   : unID = unID+1
	HOTSPOT X[i], 0, 0, unID, X[i], 2    : unID = unID+1
	HOTSPOT -1, 0, 0, unID, X[i], 3      : unID = unID+1

	paramDrawStudOffset = X[i]
	GOSUB "drawStud3d"

	for ii = 1 to 3
		if abs(horizontals[i][ii]) > nix then
			paramDrawBeamLength = X[i+1] - X[i] - paramDrawStudWidth
			HOTSPOT X[i] + paramDrawStudWidth/2, 0, horizontals[i][ii] - bottomElementHeight, unID : unID = unID+1
			HOTSPOT X[i+1] - paramDrawStudWidth/2, 0, horizontals[i][ii] - bottomElementHeight, unID : unID = unID+1

			addx X[i] + paramDrawStudWidth/2 
			addz horizontals[i][ii] - 3 * bottomElementHeight / 2

			GOSUB "drawBeam3d"
			del 2
		endif
	next ii
NEXT i

if cutHeight > NIX then
	cutend
endif


DEL 1

END

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Draw stud subrutine
! Parameters:
!	paramDrawStudOffset
!   paramDrawStudWidth
!   paramDrawStudHeight
"drawStud3d":
	ADDX paramDrawStudOffset - paramDrawStudWidth/2
	
	PRISM 4, paramDrawStudHeight,        
	paramDrawStudWidth,            B,     
	paramDrawStudWidth,            0,     
	0,            0,     
	0,            B
	
	DEL 1
RETURN

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Draw Beam Subrutine
! Parameters:
!	paramDrawBeamWidth
"drawBeam3d":
	ADDZ paramDrawBeamWidth
	ROTY 90

	! levo
	SECT_FILL fillSect1, pen_fill_bg, pen_fill, defaultStudPen
	PRISM 3, paramDrawBeamLength,	0,0, paramDrawBeamWidth, 0, paramDrawBeamWidth/2, B/2

	! desno
	SECT_FILL fillSect2, pen_fill_bg, pen_fill, defaultStudPen
	PRISM 3, paramDrawBeamLength, paramDrawBeamWidth/2, B/2, paramDrawBeamWidth, B, 0, B

	! zgoraj
	SECT_FILL fillSect3, pen_fill_bg, pen_fill, defaultStudPen
	PRISM 3, paramDrawBeamLength, 0, 0, paramDrawBeamWidth/2, B/2, 0, B

	! spodaj
	SECT_FILL fillSect4, pen_fill_bg, pen_fill, defaultStudPen
	PRISM 3, paramDrawBeamLength, paramDrawBeamWidth, 0, paramDrawBeamWidth, B, paramDrawBeamWidth/2, B/2

	!brez sekcij (rezerva)
	!PRISM 4, A, 0,0, paramDrawBeamWidth, 0, paramDrawBeamWidth, B, 0, B
	
	DEL 2
RETURN